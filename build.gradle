buildscript{
    repositories {jcenter()}
    dependencies {
        classpath 'org.jsoup:jsoup:1.7.1'
    }
}

plugins {
    id "groovy"
    id "maven"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.9.2"
    id 'net.researchgate.release' version '2.3.4'
}

repositories{
    jcenter()
}

dependencies {
    compile localGroovy()

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude group:'org.codehaus.groovy'
    }
}


sourceSets {
    main.groovy.srcDirs = ['src']
    main.resources.srcDirs = ['resources']

    test.groovy.srcDirs = ['test']
    test.resources.srcDirs = ['testResources']
}

group = "io.github.kdabir.gradlehelpers"

test {
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
}

pluginBundle {
    website = 'https://github.com/kdabir/gradle-helpers'
    vcsUrl = 'https://github.com/kdabir/gradle-helpers'
    description = 'Gradle build script helpers'
    tags = ['utility', 'helper', 'functions']

    plugins {
        gradleHelpers {
            id = 'io.github.kdabir.gradle-helpers'
            displayName = 'Gradle build script helpers'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

import org.jsoup.Jsoup

task prepareDoc << {
    def doc = Jsoup.parse(new File("build/docs/groovydoc/io/github/kdabir/gradlehelpers/HelpersExtension.html"), "UTF-8")
    def methods = doc.select('div.details ul.blockList:eq(2) ul.blockListLast > li.blockList')
    def out = methods.select('li').collect {
        def method_name = it.select('h4 > strong').html()
        def method_signature = it.select('h4').html()
        def desc = it.select('p').html()

        "- <code>$method_signature</code>\n\n" +
        "    $desc"
    }.join("\n")
    println "PASTE THE FOLLOWING IN README:\n$out"
}

prepareDoc.dependsOn  groovydoc
publishPlugins.dependsOn tasks.release
